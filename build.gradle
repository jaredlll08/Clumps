buildscript {
    repositories {
        maven {
            name = "BlameJared"
            url = 'https://maven.blamejared.com'
        }
        mavenCentral()
    }
}

plugins {
    id "fabric-loom" version "0.8-SNAPSHOT"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}
apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/secrets.gradle'

if (project.hasProperty('secretFile')) {
    loadSecrets(new File((String) findProperty('secretFile')))
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

version = 'fabric-1.17-7.0.0' + ".${getBuildNumber()}"
group = 'com.blamejared.clumps'
archivesBaseName = 'Clumps'


dependencies {
    minecraft "com.mojang:minecraft:1.17"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.11.3"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.34.9+1.17"
}


processResources {

    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {

        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {

    it.options.encoding = 'UTF-8'
    it.options.release = 16
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        maven {
            // Sets the maven URL
            url "file://" + System.getenv("local_maven")
        }
    }
}

curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    def versions = ["1.17", "1.17-Snapshot"]

    project {
        id = "256717"
        releaseType = 'release'
        changelog = "Port to Fabric"
        changelogType = 'markdown'
        addGameVersion "Fabric"

        versions.each {

            addGameVersion "${it}"
        }

        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false;
    }
}

String getBuildNumber() {

    return System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0';
}

